plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "signing"
}
repositories {
    mavenCentral()
}

group = "io.github.hiroyuki-sato"
version = "0.4.0-SNAPSHOT"
description = "Parses JSON files with JSONPath read by other file input plugins."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.31"
    compileOnly "org.embulk:embulk-spi:0.10.31"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile('org.embulk:embulk-util-json:0.1.1') {
        exclude group: "org.msgpack", module: "msgpack-core"
    }

    compile('com.jayway.jsonpath:json-path:2.4.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    //compile "joda-time:joda-time:2.9.2"

    compile "org.embulk:embulk-util-file:0.1.3"
    compile "org.embulk:embulk-util-timestamp:0.2.1"

    testCompile "junit:junit:4.13.2"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile  "org.embulk:embulk-core:0.10.31:tests"
    // 0.10.29 eliminates embulk-standards
    //testCompile  "org.embulk:embulk-standards:0.10.29"

    // TODO: Remove them.
    // These `testCompile` are a tentative workaround. It will be covered in Embulk core's testing mechanism.
    testCompile "org.embulk:embulk-deps:0.10.31"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.parser.jsonpath.JsonpathParserPlugin"
    category = "parser"
    type = "jsonpath"
}

gem {
    authors = [ "Hiroyuki Sato","Takuma kanari" ]
    email = [ "hiroysato@gmail.com","chemtrails.t@gmail.com"]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "JSON parser with JSONPath plugin for Embulk"
    homepage = "https://github.com/hiroyuki-sato/embulk-parser-jsonpath"
    licenses = [ "MIT" ]

    generateRubyCode = false  // Avoiding generate lib/embulk/parser/jsonpath.rb
    into("lib/embulk/parser/") {
      from "lib/embulk/parser/jsonpath.rb"
    }
    into("lib/embulk/guess/") {
      from "lib/embulk/guess/jsonpath.rb"
    }
    dependencies = [
      "'jsonpath', ['~> 0.5']",
      "'json', ['~> 2.0']"
    ]
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}


// For avoiding the following exception
// Caused by:
// java.lang.OutOfMemoryError: Java heap space
test {
  maxHeapSize = "2g"
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from rootProject.file("LICENSE.txt")
}

sourcesJar {
    from rootProject.file("LICENSE.txt")
}

javadocJar {
    from rootProject.file("LICENSE.txt")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://github.com/hiroyuki-sato/embulk-parser-jsonpath"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        name = "Hiroyuki Sato"
                        email = "hiroysato@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/hiroyuki-sato/embulk-parser-jsonpath.git"
                    developerConnection = "scm:git:git@github.com:hiroyuki-sato/embulk-parser-jsonpath.git"
                    url = "https://github.com/hiroyuki-sato/embulk-parser-jsonpath"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:using_gpg_agent
    useGpgCmd()
    sign publishing.publications.maven
}
